! Commands for Quarter Model with Mapped Meshing
! clear the database and setup file information
	/CLEAR
	/CWD,'C:\Users\ANSYS\QuadPlateManual'
	/FILNAME, AE6210 Assignment 5 Quad_v2
	/TITLE, AE6210 Assignment 5 Quad_v2
! define parameters
	E_length = 14,
	E_width = 2,
	E_left = 5.5,
	E_right = 8.5,
	E_thickness = .25,
	E_ym = 10100,
	E_pr = 0.33,
	C_diameter = .75,
	SG1y = 1,
	SG2y = .8790,
	SG3y = .6270,
	SG4y = .4730,
	SG5y = .3750,
	SG6x = .8750,
	SG7x = 1.6250,
	SG8x = 2.3750,
	P_force = 2000,
	T_stress = p_force/(E_width*E_thickness),
	mesh_arc = .125
	mesh_sm = .25,
	mesh_med = .5,
	mesh_lg = 1,
! reverse video color
	/RGB,INDEX,100,100,100,0,
	/RGB,INDEX,80,80,80,13,
	/RGB,INDEX,60,60,60,14,
	/RGB,INDEX,0,0,0,15,
! change amount of digits to display
	/FORMAT,,,21,12,,,
! Start Preprocessing Steps
	/PREP7
! define element types
	ET, 1, plane183,
	KEYOPT,1,3,3, ! behavior to plane stress with thickness
	R,1,E_thickness, ! define thickness of plate
! set material properties for material 1
	MP,EX,1,E_ym,	! Young's Modulus
	MP,PRXY,1,E_pr, ! Poisson's ratio
! turn on numbering
	/PNUM,kp,1,
	/PNUM,line,1,
	/PNUM,area,1,
	/PNUM,node,1,
! create keypoints to create exterior of 1/4 plate
	K,1,0,0,
	K,2,0,C_diameter/2,
	K,3,0,E_width/2,
	K,4,E_right,E_width/2,
	K,5,E_right,0,
	K,6,C_diameter/2,0,
! add lines and arc to create the shape
	L,2,3,
	L,3,4,
	L,4,5,
	L,5,6,
	LARC,6,2,1,C_diameter/2,
! create keypoints for the strain gauges
	K,11,0,SG1y,
	K,12,0,SG2y,
	K,13,0,SG3y,
	K,14,0,SG4y,
	K,15,0,SG5y,
	K,16,SG6x,0,
	K,17,SG7x,0,
	K,18,SG8x,0,
	K,19,SG8x*1.5,0,
! create second set of lines at the end of the plate
	K,21,E_right,SG1y,
	K,22,E_right,SG2y,
	K,23,E_right,SG3y,
	K,24,E_right,SG4y,
	K,25,E_right,SG5y,
	K,26,SG6x,E_width/2,
	K,27,SG7x,E_width/2,
	K,28,SG8x,E_width/2,
	K,29,SG8x*1.5,E_width/2,
! create lines connecting the nodes
	L,12,22,
	L,13,23,
	L,14,24,
	L,15,25,
	L,16,26,
	L,17,27,
	L,18,28,
	L,19,29,
! plot lines
	LPLOT
! select lines that are the full height and line 1, then create component
	LSEL,S,Length,,E_width/2,
	LSEL,A,line,,1,
	CM,VT_Lines,Line,
! select lines that are the full width and line 4, then create component
	LSEL,S,Length,,E_right,
	LSEL,A,line,,4,
	CM,HZ_Lines,Line,
! select all lines
	LSEL,all,
! split vertical lines with the horizontal lines, keep all originals
	LSBL,VT_Lines,HZ_Lines,,keep,keep,
! split horizontal lines with the vertical lines, delete all originals
	LSBL,HZ_Lines,VT_Lines,,delete,delete,
! select all lines, delete "stacked" kepoints, plot new lines
	LSEL,all,
	NUMMRG,kp,
	LPLOT
! select all lines around arc area, create fine element sizing
	LSEL,s,line,,5,
	LSEL,a,line,,14,
	LSEL,a,line,,19,20,
	LSEL,a,line,,31,32,
	LSEL,a,line,,46,
	LSEL,a,line,,48,50,
	LESIZE,all,mesh_arc,
! select all lines that are between 0 and 1, and create small element sizing
	LSEL,s,length,,0,1,
	LESIZE,all,mesh_sm,
! select all lines that are between 1 and 2, and create medium element sizing
	LSEL,s,length,,1,2,
	LESIZE,all,mesh_med,
! select all lines that are between 2 and 10, and create large element sizing
	LSEL,s,length,,2,10,
	LESIZE,all,mesh_lg,
! select all lines and plot
	LSEL,all,
	LPLOT
! create individual areas with the sets of lines
	! column 1
	AL,43,27,47,15,
	AL,47,33,48,18,
	AL,48,32,49,19,
	AL,49,31,50,14,
	AL,50,20,46,5,
	! column 2
	AL,58,28,61,27,
	AL,61,36,64,33,
	AL,64,35,67,32,
	AL,67,34,70,31,
	AL,70,21,51,20,
	! column 3
	AL,59,29,62,28,
	AL,62,39,65,36,
	AL,65,38,68,35,
	AL,68,37,71,34,
	AL,71,22,52,21,
	! column 4
	AL,60,30,63,29,
	AL,63,42,66,39,
	AL,66,41,69,38,
	AL,69,40,72,37,
	AL,72,23,53,22,
	! column 5
	AL,44,16,54,30,
	AL,54,24,55,42,
	AL,55,25,56,41,
	AL,56,26,57,40,
	AL,57,17,45,23,
! Define that mapped meshing should be used where possible, otherwise use free meshing
	MSHKEY,2,
! mesh all areas
	AMESH,all,
! change element shape checking to just create a warning instead of canceling meshing
	SHPP,WARN,
! refine all the areas by 1
	AREFINE,all,,,1,,,
! select areas for further refinement, and refine by 1
	ASEL,S,AREA,,1,20,
	AREFINE,all,,,,1,,,
! define DOF restraints
	! select lines where location x = 0, set UX DOF to 0
		LSEL,s,loc,x,0,
		DL,all,,ux,0,
	! select lines where location y = 0, set UX DOF to symmetric
		LSEL,s,loc,y,0,
		DL,all,,symm,
! select all lines and plot lines
	LSEL,all,
	LPLOT,
! select all lines with location of x=e_right
	LSEL,s,loc,x,E_right,
	! add surface load of pressure to line right side
		SFL,all,PRES,-T_stress,
 ! select everything
	ALLSEL,
! turn off nodes, plot elements
	/PNUM,node,0,
	EPLOT,
! exit the preprocessor
	FINISH
! Start Solution Processor
	/SOLU
! solve for Current LS
	SOLVE,
! eixt the solution processor
	FINISH
! Start Post Processing
	/POST1
! turn off node numbering and then plot nodal solutions for strain in x-direction
	/PNUM,node,0,
	PLNSOL,S,X,0,
! print to output the strain in X direction for nodes at the strain gage locations
	*GET,SG1sx, node,node(0,SG1Y,0),s,x
	*GET,SG2sx, node,node(0,SG2Y,0),s,x
	*GET,SG3sx, node,node(0,SG3Y,0),s,x
	*GET,SG4sx, node,node(0,SG4Y,0),s,x
	*GET,SG5sx, node,node(0,SG5Y,0),s,x
	*GET,SG6sx, node,node(SG6X,0,0),s,x
	*GET,SG7sx, node,node(SG7X,0,0),s,x
	*GET,SG8sx, node,node(SG8X,0,0),s,x
